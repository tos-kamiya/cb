#!/usr/bin/env fish

set document "\
Clipboard on pipe.

Usage 1: a-command-line | cb
  Copy. Read from the pile and save the contents to the clipboard.

Usage 2: cb | another-command-line
  Paste. Write the contents to the pipe.

Usage 3: cb
  Peek. Print the contens of the clipboard.
"

for item in $argv
    switch "$item"
        case -h --help
            echo "$document"
            exit 0
        case '*'
            echo 'Error: invalid argument / option'
            exit 1
    end
end

if not command -sq xclip
    echo 'Error: `xclip` is required, but not installed.'
    exit 1
end

if not isatty stdin
    if not isatty stdout
        echo 'Error: both ends of the `cb` can not be piped.'
        exit 1
    end

    # do copy action, when the standard input is connected to a pipe
    exec xclip -selection clipboard
else
    # do paste action, otherwise
    exec xclip -selection clipboard -o
end
